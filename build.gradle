buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/groups/public"}
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version "${axionReleaseVersion}"
}
defaultTasks 'clean','build'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'pl.allegro.tech.build.axion-release'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11
ext.rundeckPluginVersion= '1.2'
ext.rundeckVersion='5.14.0-rc1-20250722'

/**
 * Set this to a comma-separated list of full classnames of your implemented Rundeck
 * plugins.
 */
ext.pluginClassNames='org.rundeck.plugins.S3LogFileStoragePlugin'

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}
project.version = scmVersion.version

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        // maven snapshot repo since we are using SNAPSHOT of rundeck-core for now
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

// Security: Force secure versions of vulnerable dependencies
configurations.all {
    resolutionStrategy.force(
        "junit:junit:${junitVersion}",
        "org.apache.commons:commons-lang3:${commonsLang3Version}"
    )
    
    // Exclude vulnerable commons-lang 2.x in favor of commons-lang3
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'commons-lang' && details.requested.name == 'commons-lang') {
            details.useTarget group: 'org.apache.commons', name: 'commons-lang3', version: commonsLang3Version
        }
    }
}

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile{
        extendsFrom pluginLibs
    }
}

dependencies {
    // add any third-party jar dependencies you wish to include in the plugin
    // using the `pluginLibs` configuration as shown here:

    implementation "com.amazonaws:aws-java-sdk-s3:${awsSdkVersion}"
    pluginLibs (group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: "${awsSdkVersion}") {
            exclude group: "com.fasterxml.jackson.core"
            exclude group: "com.fasterxml.jackson.dataformat"
    }
    pluginLibs (group: 'com.amazonaws', name: 'aws-java-sdk-sts', version: "${awsSdkVersion}") {
            exclude group: "com.fasterxml.jackson.core"
            exclude group: "com.fasterxml.jackson.dataformat"
    }

    //the compile dependency won't add the rundeck-core jar to the plugin contents
    implementation group: 'org.rundeck', name: 'rundeck-core', version: rundeckVersion
    
    // Updated JUnit for security fixes
    testImplementation group: 'junit', name: 'junit', version: junitVersion
    
    // Force secure versions of transitive dependencies
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: commonsLang3Version
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}


jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name': 'S3 Log Plugin'
        attributes 'Rundeck-Plugin-Description': 'Store execution log files in S3, for backup or for cloud-friendly behavior.'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '5.2.0+'
        attributes 'Rundeck-Plugin-Tags': 'java,s3,execution file storage'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/rundeck-s3-log-plugin'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
        attributes 'Main-Class': "org.rundeck.plugins.S3LogFileStoragePlugin"
        attributes 'Class-Path': "${libList} lib/rundeck-core-${rundeckVersion}.jar"
    }
}
//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}